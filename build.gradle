plugins {
    id "fabric-loom" version "1.8-SNAPSHOT" apply false
    id "com.replaymod.preprocess" version "8de9ff8"
}

preprocess {
    // Here you define all versions you're supporting
    def mc1206 = createNode("1.20.6", 1_20_06, "yarn")
    def mc1211 = createNode("1.21.1", 1_21_01, "yarn")

    mc1206.link(mc1211, null)
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.tasks.named('build').get()
    }
    doFirst {
        println 'Gathering builds'
        def buildLibs = {
            p -> p.buildDir.toPath().resolve('libs')
        }
        delete fileTree(buildLibs(rootProject)) {
            include '*'
        }
        subprojects {
            copy {
                from(buildLibs(project)) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into buildLibs(rootProject)
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}
